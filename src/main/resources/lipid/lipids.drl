
package lipid;

unit LipidScoreUnit;

rule "RT positive- same type, same double bonds, more carbons "
when
    $a1 : /annotations [$rt1 : rtMin, $carbonCount1 : lipid.getCarbonCount(), $doubleBondCount : lipid.getDoubleBondsCount(),$lipidType: lipid.getLipidType()]
    $a2 : /annotations [this!= $a1, lipid.getDoubleBondsCount() == $doubleBondCount,
    rtMin > $rt1, lipid.getCarbonCount() > $carbonCount1,lipid.getLipidType() == $lipidType]
then
    // in this case, the only change is the addition of the score, but the fact does not change so we do not break the principle of refractoriness
    $a1.addScore(1);
    $a2.addScore(1);
    // !! TODO ONLY FOR DEBUGGING
    System.out.println("Scored 1 for annotations: " + $a1 + " and " + $a2);
end


// !! TODO NEXT RULE: Negative evidence. If the RT follows the opposite order than the previous one, then the addScore should be -1.


rule "RT positive- same type, less double bonds, same carbons"
when
    $a1: /annotations[$rt1: rtMin, $lipidType: lipid.getLipidType(), $carbons: lipid.getCarbonCount(), $db1: lipid.getDoubleBondsCount()]
    $a2: /annotations[
        this != $a1,
        lipid.getLipidType() == $lipidType,
        lipid.getCarbonCount() == $carbons,
        lipid.getDoubleBondsCount() > $db1,
        rtMin < $rt1
    ]
then
    $a1.addScore(1);
    $a2.addScore(1);
    System.out.println("Scored 1 for annotations: " + $a1 + " and " + $a2);
end


rule "RT positive - different type, same double bonds, same carbons, RT follows PG < PE < PI < PA < PS < PC"
when
    $a1 : /annotations [
        $carbons1 : lipid.getCarbonCount(),
        $db1 : lipid.getDoubleBondsCount(),
        $rt1 : rtMin,
        $l1 : lipid
    ]
    $a2 : /annotations [
        this != $a1,
        lipid.getCarbonCount() == $carbons1,
        lipid.getDoubleBondsCount() == $db1,
        $rt1 > rtMin,
        $l2 : lipid
    ]
    eval($l1.getLipidTypeRank() > $l2.getLipidTypeRank())
then
    $a1.addScore(1);
    $a2.addScore(1);
    System.out.println("Scored 1 for annotations: " + $a1 + " and " + $a2);
end
// !! TODO NEXT RULE: Negative evidence. If the RT follows the opposite order than the previous one, then the addScore should be -1.


rule "RT negative - same type, same double bonds, more carbons"
salience 10
when
    $a1: /annotations[$rt1: rtMin,$lipidType: lipid.getLipidType(),$db: lipid.doubleBondsCount,$carbons1: lipid.carbonCount]
    $a2: /annotations[this != $a1,lipid.getLipidType() == $lipidType,lipid.doubleBondsCount == $db,lipid.carbonCount > $carbons1,rtMin < $rt1]
then
    $a1.addScore(-1);
    $a2.addScore(-1);
    System.out.println("Scored -1 for annotation: " + $a2);
end


rule "RT negative - same type, less double bonds, same carbons"
salience 10
when
    $a1: /annotations[$rt1: rtMin, $lipidType: lipid.getLipidType(), $carbons: lipid.getCarbonCount(), $db1: lipid.getDoubleBondsCount()]
    $a2: /annotations[
        this != $a1,
        lipid.getLipidType() == $lipidType,
        lipid.getCarbonCount() == $carbons,
        lipid.getDoubleBondsCount() > $db1,
        rtMin > $rt1
    ]
then
    $a1.addScore(-1);
    $a2.addScore(-1);
    System.out.println("Scored -1 for annotation: " + $a2);
end


rule "RT negative - different type, same carbons/double bonds, RT follows PG < PE < PI < PA < PS < PC"
when
    $a1 : /annotations [$carbons1 : lipid.getCarbonCount(), $db1 : lipid.getDoubleBondsCount(), $rt1 : rtMin, $l1 : lipid]
    $a2 : /annotations [this != $a1, lipid.getCarbonCount() == $carbons1, lipid.getDoubleBondsCount() == $db1, $rt1 < rtMin, $l2 : lipid]
    eval($l1.getLipidTypeRank() > $l2.getLipidTypeRank())
then
    $a1.addScore(-1);
    $a2.addScore(-1);
    System.out.println("Scored -1 for annotations: " + $a1 + " and " + $a2);

end